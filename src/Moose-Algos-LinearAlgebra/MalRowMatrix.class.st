"
Copyright (c), 2004-2007 Adrian Kuhn. This class is part of Hapax. Hapax is distributed under BSD License, see package comment.
"
Class {
	#name : #MalRowMatrix,
	#superclass : #MalMatrix,
	#instVars : [
		'rows'
	],
	#category : #'Moose-Algos-LinearAlgebra'
}

{ #category : #private }
MalRowMatrix class >> onRows: rowCollection [
	| new |
	new := self basicNew.
	new rows: rowCollection asArray.
	[ new checkInvariant ] assert.
	^ new
]

{ #category : #accessing }
MalRowMatrix >> at: row and: column [
	
	^(rows at: row) at: column
]

{ #category : #accessing }
MalRowMatrix >> at: row and: column put: aNumber [
	
	^(rows at: row) at: column put: aNumber
]

{ #category : #private }
MalRowMatrix >> checkInvariant [
	
	^rows isCollection
		and:
			[(rows allSatisfy: [:row | row isKindOf: self vectorSpecies])
				and: [rows allSatisfy: [:row | row size = self columnSize]]]
]

{ #category : #accessing }
MalRowMatrix >> columnSize [
	^ rows ifEmpty: [ 0 ] ifNotEmpty: [ rows first size ]
]

{ #category : #initialization }
MalRowMatrix >> initializeRows: m columns: n [
	
	rows := Array new: m.
	1
		to: m
		do:
			[:index | 
			rows
				at: index
				put: (self vectorSpecies new: n)]
]

{ #category : #'accessing vectorwise' }
MalRowMatrix >> rowAt: rowIndex [
	
	^rows at: rowIndex
]

{ #category : #accessing }
MalRowMatrix >> rowSize [
	
	^rows size
]

{ #category : #'accessing vectorwise' }
MalRowMatrix >> rows [
	
	^rows
]

{ #category : #accessing }
MalRowMatrix >> rows: anObject [
	rows := anObject
]
